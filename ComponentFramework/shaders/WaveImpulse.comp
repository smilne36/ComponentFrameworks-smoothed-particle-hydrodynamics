#version 450
layout(local_size_x=256) in;

struct Particle {
    vec4 pos; vec4 vel; vec4 acc;
    float density; float pressure; float padA; float padB;
    ivec4 flags; // x=isGhost
};
layout(std430, binding=0) buffer ParticleBuf { Particle particles[]; };

layout(std140, binding=5) uniform WaveParams {
    float amplitude;
    float wavelength;
    float phase;
    vec3  dir;
    float yMin;
    float yMax;
};

void main() {
    uint i = gl_GlobalInvocationID.x;
    Particle p = particles[i];
    if (p.flags.x != 0) return; // skip ghosts
    if (p.pos.y < yMin || p.pos.y > yMax) return;
    float k = (wavelength > 1e-6) ? (2.0*3.14159265 / wavelength) : 0.0;
    float theta = k * (p.pos.x + p.pos.z) + phase;
    float kick  = amplitude * sin(theta);
    vec3 nDir = (length(dir) > 0.0) ? normalize(dir) : vec3(0,1,0);
    p.vel.xyz += nDir * kick;
    particles[i] = p;
}