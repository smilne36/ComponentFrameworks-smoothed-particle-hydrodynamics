#version 450
layout(local_size_x = 256) in;

// Match CPU side SPHParticle layout (80 bytes)
struct SPHParticle {
    vec4  pos;
    vec4  vel;
    vec4  acc;
    float density;
    float pressure;
    float padA;
    float padB;
    int   isGhost;
    int   isActive;
    int   padC;
    int   pad0;
};

layout(std430, binding = 0) buffer ParticleBuf { SPHParticle particles[]; };

// Uniforms expected by ApplyWaveImpulse() in SPHFluid3D.cpp
uniform int   N;
uniform float amplitude;
uniform float wavelength;
uniform float phase;
uniform vec3  dir;
uniform float yMin;
uniform float yMax;

void main() {
    uint i = gl_GlobalInvocationID.x;
    if (i >= uint(N)) return;

    SPHParticle p = particles[i];
    if (p.isGhost != 0) return;
    if (p.pos.y < yMin || p.pos.y > yMax) return;
    if (amplitude == 0.0 || wavelength <= 1e-6) return;

    vec3 nDir = (length(dir) > 1e-6) ? normalize(dir) : vec3(0.0, 1.0, 0.0);
    float k = 6.28318530718 / wavelength;          // 2π / λ
    float theta = k * dot(p.pos.xyz, nDir) + phase;
    float kick  = amplitude * sin(theta);

    p.vel.xyz += nDir * kick;
    particles[i] = p;
}